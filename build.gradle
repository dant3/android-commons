plugins {
    id 'java'
    id 'maven'
    id 'maven-publish'
    id "com.jfrog.bintray" version "1.0"
    id 'nebula.provided-base' version '2.0.1'
    // -- analysis
    id 'pmd'
    id 'findbugs'
    id 'jacoco'
}

// --- project settings ---
version = "0.1"

// --- eof project settings ---

apply {
    from 'gradle/java-bootclasspath.gradle'
    // --- for tests ---
    from 'gradle/jacoco.gradle'
    from 'gradle/findbugs-console-output.gradle'
    from 'gradle/scala.gradle'
}

ext {
    // whenever the logs should write logging output or not
    testsLogging = false
}

scala {
    version '2.11.5'
}

// --=[ javac opts ]=--
compileJava {
    sourceCompatibility = 1.6
    targetCompatibility = 1.6
    options.deprecation = true
    options.encoding = 'UTF-8'
    options.incremental = true
    options.compilerArgs = [ "-Werror", "-Xlint:all", "-Xlint:-processing" ]
}

repositories {
    mavenCentral()
}

dependencies {
    provided group: "com.google.android", name: "android", version: "4.1.1.4"
    provided group: 'org.projectlombok', name: 'lombok', version:'1.14.6'

    compile group: 'com.google.guava', name: 'guava', version:'18.0'

    // ----------------------------
    // ---| TEST  DEPENDENCIES |---
    // ----------------------------
    compile scalaLang()
    testCompile scalaLang()
    testCompile scalaLibrary(group: 'org.scalatest', name: 'scalatest', version: '2.2.1')

    testCompile group: 'org.mockito', name: 'mockito-core', version:'1.9.5'
}


// --- analysis ---
test {
    // ignoreFailures = true // FIXME
    enableAssertions = true
    finalizedBy jacocoTestReport

    testLogging {
        events = ["failed"]
        if (testsLogging) { // FIXME: workaround of bug in Gradle, fixed in 2.4 only
            events = ["started", "skipped", "failed"]
            showStandardStreams = testsLogging
        }
        showExceptions = true
        showCauses = true
        exceptionFormat = "full"
    }
}

pmd {
    consoleOutput = true
    ignoreFailures = false
    ruleSetFiles = files("pmd-rules.xml")
    sourceSets = [ sourceSets.main ] // only analyze main sources
    toolVersion = '5.0.3'
}

findbugs {
    toolVersion = '3.0.1-rc1'
    ignoreFailures = false
    effort = "default"
    reportLevel = "medium"
}

findbugsTest.onlyIf { ext.hasProperty('analyzeTests') && ext.analyzeTests == true }

jacoco {
    toolVersion = "0.7.1.201405082137"
    excludes = [ "org.sqlite.SQLite" ]
}


jacocoTestReport {
    group = "Reporting"
    description = "Generate Jacoco coverage reports after running tests."
    enabled true
    shouldRunAfter test

    reports {
        xml {
            enabled true
            destination "${buildDir}/reports/coverage/jacoco.xml"
        }
        html {
            enabled true
            destination "${buildDir}/reports/coverage/"
        }
    }
}

// ---> deployment

bintray {
    // user and key come from ~/.gradle/gradle.properties
    user = bintray_user
    key = bintray_key

    publications = ['mavenJava']
    publish = true
    dryRun = false
    pkg {
        repo = 'maven'
        name = 'android-commons'
        desc = 'Commonly used android utils'
        websiteUrl = 'https://github.com/dant3/android-commons'
        issueTrackerUrl = 'https://github.com/dant3/android-commons/issues'
        vcsUrl = 'https://github.com/dant3/android-commons.git'
        licenses = ['Apache-2.0']
        labels = ['android']
        publicDownloadNumbers = true
//        attributes= ['a': ['ay1', 'ay2'], 'b': ['bee'], c: 'cee'] //Optional package-level attributes
        //Optional version descriptor
        version {
            name = project.version
//            desc = 'optional, version-specific description'
//            released  = 'optional, date of the version release' //2 possible values: date in the format of 'yyyy-MM-dd'T'HH:mm:ss.SSSZZ' OR a java.util.Date instance
//            vcsTag = '1.3.0'
//            attributes = ['gradle-plugin': 'com.use.less:com.use.less.gradle:gradle-useless-plugin'] //Optional version-level attributes
//            gpg {
//                sign = true //Determines whether to GPG sign the files. The default is false
//                passphrase = 'passphrase' //Optional. The passphrase for GPG signing'
//            }
        }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
            groupId "com.github.dant3"
            artifactId "android-commons"
            version project.version
        }
    }
    repositories {
        // Create an ivy publication destination named “releases”
        maven {
            name "maven"
            url "http://dl.bintray.com/dant3/maven"
        }
    }
}
